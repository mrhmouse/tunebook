song "example song"
tempo 45

voice "chirp"
modulate 4/5
groove (5) r
groove (5/7)
section
section
 3/2 r 3/2 r
 9/8 6/5 9/8
repeat 1
section
 4/3 r 8/5 3/2
 9/8 6/5 4/3
 4/3 r 3/2 8/5
 6/5 9/8 9/10
repeat 1
repeat 1
 2/3

voice "snare"
modulate 3
groove (1/4)
section
r r 1 r
r 1 1 r
r r 1 r
r r 1 1
r r 1 r
repeat 8

voice "kick"
groove (1/4)
modulate 5
section
 1 r 1 r
 1 r 1 r
 1 r 1 r
 1 r 1 r
 1 r 1 r
repeat 8

voice "stab"
modulate 2/5
groove (1/2)
section
 r (6/5 3/2 9/5)
 r (9/8 3/2 9/5)
 r (4/3 24/15 2)
 r (4/3 24/15 9/5)
 r (6/5 24/15 9/5)
repeat 8
groove (2)
modulate 1/2
 (6/5 3/2 9/5) r
 (9/8 3/2 9/5) r
 (4/3 24/15 2) r
 (4/3 24/15 9/5) r
 (6/5 24/15 9/5) r

voice "bass"
groove (1/2)
modulate 1/5
section
 1 r
 1 r
 1 8/15
 4/5 r
 4/5 3/8
repeat 8

instrument "stab"
sine "a" attack 3/4 decay 1/16 sustain 1/5 release 4 volume 1/15
sine "b" attack 2/3 decay 1/2 sustain 1/2 release 1/2 volume 1 fm ("a")
sine "c" attack 2/3 decay 1/2 sustain 2/3 release 4 volume 1/5 hz 4 fm ("a" "c") am ("a")

instrument "bass"
tri "a" attack 3/4 decay 1/8 sustain 1/16 release 1/32 volume 1/3
tri "b" attack 1/2 decay 1/2 sustain 1/8 release 1/32 volume 1 detune 1/2 fm ("a")
tri "c" attack 1 decay 1/3 sustain 1/8 release 1/32 volume 1 hz 3/2 fm ("a")
tri "d" attack 3/4 decay 3/4 sustain 1/8 release 1/32 volume 1 hz 5/3 fm ("b" "c")

instrument "kick"
sine "a" attack 0 sustain 1/3 decay 1/2 release 1 volume 2/5 hz 55
square "b" attack 0 sustain 0 decay 1/8 release 0 volume 80 hz 1/1000 fm ("a")
noise "n" attack 0 sustain 0 decay 1/4 release 0 volume 1/4 fm ("a")

instrument "snare"
noise "a" attack 0 sustain 1/32 decay 2/3 release 1 volume 1/20
saw "s" attack 0 sustain 0 decay 2/3 release 0 volume 1 fm ("a")

instrument "chirp"
sine "a" attack 1/64 decay 2/3 sustain 1/3 release 1/3 volume 1/15
square "b" attack 1/2 decay 1/2 sustain 1/2 release 1/3 volume 1/4 hz 7 am ("a")
sine "c" attack 1/64 decay 2/3 sustain 1/3 release 1/3 volume 30 pm ("a")